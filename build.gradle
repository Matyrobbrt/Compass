import org.parchmentmc.compass.tasks.GenerateExport

import java.time.OffsetDateTime
import java.time.ZoneOffset

plugins {
    id 'org.parchmentmc.compass'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    maven { url = "https://maven.minecraftforge.net/" }
}

compass {
    version = '1.16.5'
}

dependencies {
    mcpconfig 'de.oceanlabs.mcp:mcp_config:1.16.5-20210115.111550'
}

def now = OffsetDateTime.now(ZoneOffset.UTC)
def dateTime = now.format("yyyyMMdd-HHmmss")

publishing {
    publications.create('datedExport', MavenPublication) { p ->
        p.artifact(tasks.named("generateOfficialExport", GenerateExport).flatMap { it.output }) {
            classifier = null
        }
        p.artifact(tasks.named("generateSrgExport", GenerateExport).flatMap { it.output }) {
            classifier = "srg"
        }
        p.groupId = "org.parchmentmc.mappings"
        afterEvaluate {
            p.artifactId = compass.version.get()
            p.version = dateTime
        }
    }
    publications.create('datedStagingExport', MavenPublication) { p ->
        p.artifact(tasks.named("generateOfficialStagingExport", GenerateExport).flatMap { it.output }) {
            classifier = null
        }
        p.artifact(tasks.named("generateSrgStagingExport", GenerateExport).flatMap { it.output }) {
            classifier = "srg"
        }
        p.groupId = "org.parchmentmc.mappings"
        afterEvaluate {
            p.artifactId = compass.version.get()
            p.version = "staging-SNAPSHOT"
        }
    }
    repositories {
        maven {
            name "projectLocal"
            url "file://${project.file('repo').absolutePath}"
        }
    }
}